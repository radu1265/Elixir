defmodule WineCellar do
  def explain_colors do
    [white: "Fermented without skin contact.",
    red: "Fermented with skin contact using dark-colored grapes.",
    rose: "Fermented with some skin contact, but not enough to qualify as a red wine."]
  end

  def filter(cellar, color, opts \\ [])
  def filter(cellar, color, []), do: Keyword.get_values(cellar, color)
  def filter(cellar, color, opts) do
    Keyword.get_values(cellar, color)
    |> filter_wines(opts)
  end

  defp filter_wines(wines, []), do: wines
  defp filter_wines(wines, [head | tail]) do
    wines
    |> apply_filter(head)
    |> filter_wines(tail)
  end

  defp apply_filter(wines, {:country, country}) do
    filter_by_country(wines, country)
  end
  
  defp apply_filter(wines, {:year, year}) do
    filter_by_year(wines, year)
  end
  defp apply_filter(wines, _), do: wines
  
  # The functions below do not need to be modified.

  defp filter_by_year(wines, year)
  defp filter_by_year([], _year), do: []

  defp filter_by_year([{_, year, _} = wine | tail], year) do
    [wine | filter_by_year(tail, year)]
  end

  defp filter_by_year([{_, _, _} | tail], year) do
    filter_by_year(tail, year)
  end

  defp filter_by_country(wines, country)
  defp filter_by_country([], _country), do: []

  defp filter_by_country([{_, _, country} = wine | tail], country) do
    [wine | filter_by_country(tail, country)]
  end

  defp filter_by_country([{_, _, _} | tail], country) do
    filter_by_country(tail, country)
  end
end
